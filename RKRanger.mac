#Event SnareImmune "#*#Your target is immune to changes in its run speed."

#include RKBase.inc
#include RKMeleeBase.inc
#include RKCasterBase.inc
#include RKHealerBase.inc
|#include RKCrowdControlBase.inc

Sub Main
	/call Init_RKBase
	/call Init_RKMeleeBase
	/call Init_RKHealerBase
	|/call Init_RKCrowdControlBase
	/call Init_RKCasterBase
		
| *** VARIABLE DECLARATIONS
	/varset SpellMemSlot 11
	
	/varset StickFlags behind
	
	|/varset CCMode FALSE
	|/varset SingleTargetMez Flusterbolt
	|/varset SingleTargetMez2 Blusterbolt Rk. II
	|/varset MezSub RangerCC
	|/varset SingleTargetMezLevelCap 120

	/varset InCombatStickToCommander FALSE
	
	/declare SnareMode bool outer TRUE
	/declare ShouldSnare bool outer TRUE	

	/varset ShouldBroadcastMed FALSE
	
	/varset KeepAgroOnAdds FALSE

	/varset ForageMode FALSE
	/varset PullWithAutoFire TRUE

	/varset UpperAggroThreshold 100

	/varset HealSpell Kromtus Balm Rk. II
	/varset EmergencyHealSpell Desperate Dousing Rk. II
	/varset TankHealThreshold 50
	/varset SingleTargetHealThreshold 50
	/varset GroupHealThreshold 0
		
	/call AddToArray "CombatActionSubs" "ExecuteRangerCombatActions"
	/call AddToArray "DefensiveActionSubs" "ExecuteDefensiveActions"		
	/call AddToArray "NonCombatSubs" "RangerNonCombatSubs"
	
	/call AddToArray "InvisModeSubs" "PeriodicGroupInvis"
	
	/call AddToArray "DetrimentalSpells" "Anticipated Shots Rk. II"
	/call AddToArray "DetrimentalSpells" "Focused Arrowrain Rk. II"
	/call AddToArray "DetrimentalSpells" "Swarm of Glistenwings Rk. II"

		
	/call AddToArray "GiftSpells" "Summer's Cyclone Rk. II"
	
	/call AddToArray "NonCombatSelfBuffs" "Devastating Swords Rk. II"
	/call AddToArray "NonCombatSelfBuffs" "Copsestalker's Enrichment Rk. III"	

	/call AddToArrayWithUniqueName "NonCombatSelfBuffs" "Copsestalker's Unity Rk. II" "Energized by the Hunt Rk. II"
	/call AddToArrayWithUniqueName "NonCombatSelfBuffs" "Copsestalker's Unity Rk. II" "Protection of the Forest Rk. II"
	/call AddToArrayWithUniqueName "NonCombatSelfBuffs" "Copsestalker's Unity Rk. II" "Eyes of the Harrier Rk. II"
	/call AddToArrayWithUniqueName "NonCombatSelfBuffs" "Copsestalker's Unity Rk. II" "Nettlespear Coat Rk. II"
	
	/call AddToArray "BurnCombatActionSubs" "RangerBurn"
	/call AddToArray "XPEarnedSubs" "ResetSnareMode" 
	
	/echo Done initializing ranger macro!

| *** MAIN LOOP

:loop
	/call Tick
	/goto :loop
/return

Sub RangerCC(ccTargetId)
	/if (${Me.SpellReady[FlusterBolt]}) /call RKCast "Flusterbolt" ${ccTargetId}
	/if (${Me.SpellReady[Blusterbolt Rk. II]}) /call RKCast "Blusterbolt Rk. II" ${ccTargetId}	
	/if (${Spawn[id ${ccTargetId}].Distance} > 100) {
		/call RKAACast "Grasp of Sylvan Spirits" ${ccTargetId}
	}
	
/return FALSE

Sub ResetSnareMode
	/if (${SnareMode}) /varset ShouldSnare TRUE
/return FALSE

Sub RangerNonCombatSubs
	
/return FALSE

Sub RangerBurn
	|/call RKDebuffCast "Blisterbeetle Swarm Rk. II" "Blisterbeetle Swarm Rk. II" ${CurrentTargetId}
	/call RKAACast "Fundament: Third Spire of the Pathfinders" ${CurrentTargetId}
	/call RKAACast "Auspice of the Hunter" ${CurrentTargetId}
	/call RKAACast "Group Guardian of the Forest" ${CurrentTargetId}
	/call RKAACast "Imbued Ferocity" ${CurrentTargetId}
	
	/call RKCast "Dichotomic Fusillade" ${CurrentTargetId}

	/call RKDisc "Reflexive Bladespurs Rk. II"
	/disc "Copsestalker's Discipline Rk. II"
	|/call RKDisc "Warder's Wrath"
	
	/call RKCast "Wildfire Boon Rk. II" ${CurrentTargetId}
	/call RKCast "Wildfire Ash Rk. II" ${CurrentTargetId}
	/call RKCast "Frozen Wind Rk. II" ${CurrentTargetId}	
	
/return FALSE

Sub ExecuteRangerCombatActions
	/if (${Target.Type.Equal[PC]}) /return FALSE

	/if (${Target.Distance} < 25) {
		/doability 1
		/doability 2
	}
	
	/if (${Me.PctMana} > 50) {
		/call RKCast "Dichotomic Fusillade" ${CurrentTargetId}
	}
	
	/if (${Me.XTarget[3].ID} || ${Me.Song[Prophet's Gift of the Ruchu].ID}) {
		/call RKDisc "Copsestalker's Discipline Rk. II"
		/if (${Macro.Return}) /return FALSE
		/call RangerBurn
	}
	
	/if (${Target.Distance} > 30 && !${Me.AutoFire}) /autofire
	
	/if (${Target.Distance} < 15 && ${Stick.Active}) /stick off

	/if (${Me.PctMana} > 50) {
		/call RKCast "Wildfire Boon Rk. II" ${CurrentTargetId}
		/call RKCast "Wildfire Ash Rk. II" ${CurrentTargetId}
		/call RKCast "Frozen Wind Rk. II" ${CurrentTargetId}	
	}
	
	/call RKAACast "Guardian of the Forest" ${CurrentTargetId}
	
	/if (!${Me.Moving}) {
		/if (${SnareMode} && ${ShouldSnare} && !${Target.Buff[Entrap].ID} && ${Target.Distance} < 30) /call RKAACast "Entrap"
		/if (${Target.Distance} < 25) {
			/if (${Me.ID} == ${TankId}) {
				/call RKDisc "Enraging Cut Kicks Rk. III"
			} else {
				/call RKDisc "Jolting Cut Kicks Rk. III"
			}
		}

		/call RKDisc "Focused Storm of Blades Rk. II"
		|/if (${Me.XTarget[2].ID}) {
		|	/call RKDisc "Squall of Blades Rk. II"
		|} else {
		|}
	}
	
	/call RKCast "Anticipated Shots Rk. II" ${CurrentTargetId}
	/call RKCast "Focused Arrowrain Rk. II" ${CurrentTargetId}
	/call RKCast "Swarm of Glistenwings Rk. II" ${CurrentTargetId}
	
	/if (${Target.PctHPs} < 30) /return FALSE
	
	/call RKAACast "Scout's Mastery of Fire" ${CurrentTargetId}
	/call RKAACast "Glacial Arrow" ${CurrentTargetId}
	/call RKAACast "Volatile Arrow" ${CurrentTargetId}
	/call RKAACast "Outrider's Accuracy" ${CurrentTargetId}
	/call RKAACast "Outrider's Attack" ${CurrentTargetId}
	/call RKAACast "Pack Hunt" ${CurrentTargetId}
	/call RKItemSlotCast chest

/return FALSE

Sub PeriodicGroupInvis
	/if (!${Me.Invis}) /call RKAACast "Shared Camouflage" ${CurrentTargetId}
	/if (${DebugMode}) /echo Group Invis
/return FALSE

Sub ExecuteDefensiveActions
	/if (${Me.PctHPs} < 50) /call RKDisc "Weapon Shield Discipline"
	/if (${Bool[${Macro.Return}]} && ${Macro.Return}) /return TRUE	

	|/if (${Me.PctHPs} < 70) /call RKAACast "Guardian of the Forest"
	|/if (${Bool[${Macro.Return}]} && ${Macro.Return}) /return TRUE	
	
/return FALSE

Sub Event_SnareImmune
	/varset ShouldSnare FALSE
/return
